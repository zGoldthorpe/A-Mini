; from Gargi "A Sparse Algorithm for Predicated Global Value Numbering"
; Figure 2

@R:
    read %x.0
    read %y.0
    read %z.0

@1:
    %i.1 = 1
    %j.1 = 1
    %1.4 = 1
    branch %1.4 ? @2 : @18

@2:
    %i.2 = phi [ %i.1, @1 ], [ %i.17, @17 ]
    %j.2 = phi [ %j.1, @1 ], [ %j.3, @17 ]
    %2.3 = 9 < %j.2
    branch %2.3 ? @18 : @3

@3:
    %j.3 = %j.2 + 1
    %3.2 = %i.2 != 1
    branch %3.2 ? @4 : @5

@4:
    %i.4 = 2

@5:
    %i.5 = phi [ %i.2, @3 ], [ %i.4, @4 ]
    %5.2 = %x.0 == %y.0
    branch %5.2 ? @6 : @17

@6:
    %p.6 = 0
    %6.2 = 1 <= %x.0
    branch %6.2 ? @7 : @11

@7:
    %7.1 = %i.5 != 1
    branch %7.1 ? @8 : @9

@8:
    %p.8 = 2
    goto @11

@9:
    %9.1 = %x.0 <= 9
    branch %9.1 ? @10 : @11

@10:
    %p.10 = %i.5

@11:
    %p.11 = phi [ %p.6, @6 ], [ %p.8, @8 ], [ %p.6, @9 ], [ %p.10, @10 ]
    %q.11 = 0
    %11.3 = %i.5 <= %y.0
    branch %11.3 ? @12 : @14

@12:
    %12.1 = %y.0 <= 9
    branch %12.1 ? @13 : @14

@13:
    %q.13 = 1

@14:
    %q.14 = phi [ %q.11, @11 ], [ %q.11, @12 ], [ %q.13, @13 ]
    %14.2 = %i.5 < %z.0
    branch %14.2 ? @15 : @16

@15:
    %15.1.0 = %x.0 + 2
    %sum.0 = %p.11 + %15.1.0
    %15.1.1 = %z.0 < 1
    %sum.1 = %sum.0 + %15.1.1
    %15.1.2 = %i.5 + %y.0
    %sum.2 = %sum.1 - %15.1.2
    %i.15 = %sum.2 - %q.14

@16:
    %i.16 = phi [ %i.5, @14 ], [ %i.15, @15 ]

@17:
    %i.17 = phi [ %i.5, @5 ], [ %i.16, @16]
    goto @2

@18:
    %i.18 = phi [ %i.1, @1 ], [ %i.2, @2 ]
    write %i.18

